plugins {
    id 'com.android.application'
}

android {
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    if (keystorePropertiesFile.exists()) {
        def keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
        signingConfigs {
            release {
                storeFile file(keystoreProperties.releaseStoreFilePath)
                storePassword keystoreProperties.releaseStorePassword
                keyAlias keystoreProperties.releaseKeyAlias
                keyPassword keystoreProperties.releaseKeyPassword
            }
        }
    }
    compileSdkVersion 30
    defaultConfig {
        applicationId 'pl.com.karwowsm.musiqueue'
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName '1.0'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {

        def addConstants = { target, schema, host, port ->
            target.buildConfigField 'String', 'SERVER_HOST', "\"${host}\""
            target.buildConfigField 'String', 'SERVER_PORT', "\"${port}\""
            target.buildConfigField 'String', 'SERVER_ADDRESS', 'BuildConfig.SERVER_HOST + ":" + BuildConfig.SERVER_PORT'
            target.buildConfigField 'String', 'BASE_URL', "\"${schema}://\"  + BuildConfig.SERVER_ADDRESS"
        }

        debug {
            addConstants(owner, 'https', 'musiqueue.herokuapp.com', '443')
        }
        dev {
            initWith debug

            addConstants(owner, 'http', InetAddress.localHost.hostAddress, '8080')
        }
        release {
            addConstants(owner, 'https', 'musiqueue.herokuapp.com', '443')

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }

        all {
            buildConfigField 'String', 'YOUTUBE_API_KEY', youTubeApiKey
            buildConfigField 'String', 'SPOTIFY_CLIENT_ID', spotifyClientId
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'com.google.android.exoplayer:exoplayer-core:2.12.1'
    implementation 'com.google.android.exoplayer:exoplayer-hls:2.12.1'
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.github.NaikSoftware:StompProtocolAndroid:1.6.4'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    compileOnly 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'

    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.0'
}
